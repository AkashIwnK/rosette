#lang racket

(require 
  racket/generator
  "eval.rkt" 
  (only-in "../base/core/term.rkt" constant? get-type term? term-cache clear-terms! term<?)
  (only-in "../base/core/equality.rkt" @equal?)
  (only-in "../base/core/bool.rkt" ! || && => with-asserts-only @boolean?)
  (only-in "../base/core/real.rkt" @integer? @real?)
  (only-in "../base/core/bitvector.rkt" bv bitvector?)
  (only-in "../base/core/finitize.rkt" finitize current-bitwidth)
  "../solver/solver.rkt"
  (only-in "../solver/solution.rkt" model core sat unsat sat? unsat? default-binding)
  (only-in "../solver/smt/z3.rkt" z3))

(provide current-solver ∃-solve ∃-solve+ ∃∀-solve ∃-debug eval/asserts 
         all-true? some-false? unfinitize)

; Current solver instance that is used for queries and kept alive for performance.
(define current-solver
  (make-parameter (z3)
                  (lambda (s)
                    (unless (solver? s)
                      (error 'current-solver "expected a solver?, given ~s" s))
                    (solver-shutdown (current-solver))
                    s)))

; Returns true if evaluating all given formulas against 
; the provided solution returns the constant #t.
(define (all-true? φs sol)  
  (and (sat? sol) (for/and ([φ φs]) (equal? #t (evaluate φ sol)))))

; Returns true if evaluating at least one of the given 
; formulas against the provided solution returns the constant #f.
(define (some-false? φs sol)
  (and (sat? sol) (for/or ([φ φs]) (false? (evaluate φ sol)))))

(define return-#f (const '(#f)))

; Returns the list of assertions generated by evaluating the given thunk. 
; If the evaluation of the thunk results in an exn:fail? exception, returns 
; the list '(#f).
(define (eval/asserts closure)
  (with-handlers ([exn:fail? return-#f])
    (with-asserts-only (closure))))

; Takes as input a solution and a finitization map 
; produced by calling the finitize proceudre in rosette/base/core/finitize, 
; and returns a new solution that applies the inverse 
; of the given to the provided solution.   
(define (unfinitize sol fmap)
  (match sol
    [(model m)
     (sat (for/hash ([(k fk) fmap] #:when (dict-has-key? m fk))
            (match* ((get-type k) (dict-ref m fk))
              [((or (== @integer?) (== @real?)) (bv v _)) (values k v)]
              [(_ v) (values k v)])))]
    [(core #f) sol] ; no core extracted
    [(core φs)
     (unsat (for/list ([(k v) fmap] #:when (member v φs)) k))]))

; Takes as input a solution and a finitization map 
; produced by calling the finitize proceudre in rosette/base/core/finitize, 
; and returns a solution that is complete with respect to the given map.  
; That is, if the given solution is satisfiable but has no mapping for a
; constant in fmap, the returned solution has a default binding  
; for that constant (and same bindings as sol for other constants).  If 
; the given solution is unsat, it is simply returned.
(define (complete sol fmap)
  (match sol
    [(model m)
     (sat (for/hash ([(k fk) fmap] #:when (constant? k))
            (values fk (dict-ref m fk (default-binding fk)))))]
    [_ sol]))
  

; Searches for a model, if any, for the conjunction 
; of the given formulas, using the provided solver and 
; bitwidth.  The solver and the bitwidth are, by default, 
; current-solver and current-bitwidth.  Returns an unsat 
; solution if the given formulas don't have a model with 
; the specified bitwidth that is also correct under the 
; precise semantics. This procedure clears the solver's state 
; before and after use.
(define (∃-solve φs
                 #:minimize [mins '()]
                 #:maximize [maxs '()]
                 #:solver [solver (current-solver)]
                 #:bitwidth [bw (current-bitwidth)])
  (solver-clear solver)
  (begin0  
    (with-handlers ([exn? (lambda (e) (solver-shutdown solver) (raise e))])
      (cond 
        [bw 
         (parameterize ([term-cache (hash-copy (term-cache))])
           (define fmap (finitize (append φs mins maxs) bw))
           (solver-assert solver (for/list ([φ φs]) (hash-ref fmap φ)))
           (solver-minimize solver (for/list ([m mins]) (hash-ref fmap m)))
           (solver-maximize solver (for/list ([m maxs]) (hash-ref fmap m)))
           (let loop ()
             (define fsol (complete (solver-check solver) fmap))
             (define sol (unfinitize fsol fmap)) 
             (cond 
               [(or (unsat? sol) (all-true? φs sol)) sol]
               [else (solver-assert solver (list (apply || (for/list ([(c v) (model fsol)]) (! (@equal? c v))))))
                     (loop)])))]
        [else 
         (solver-assert solver φs)
         (solver-minimize solver mins)
         (solver-maximize solver maxs)
         (solver-check solver)]))
    (solver-clear solver)))

; Returns a generator that uses the solver of the given type, with the given 
; bitwidth setting, to incrementally solve a series of constraints.  The generator 
; consumes lists of constraints (i.e., boolean values and terms), and produces a 
; sequence of solutions.  Specifically, the ith returned solution is a solution for 
; all constraints added to the generator in the preceding i-1 calls.
(define (∃-solve+ #:solver [solver-type z3] #:bitwidth [bw (current-bitwidth)])
  (define cust (make-custodian))
  (define solver (parameterize ([current-custodian cust]
                                [current-subprocess-custodian-mode 'kill])
                   (solver-type)))
  (define handler (lambda (e) (solver-shutdown solver) (custodian-shutdown-all cust) (raise e)))
  (if bw
      (generator (ψs)
       (let ([fmap (make-hash)]
             [φs '()])
         (let outer ([δs ψs])
           (with-handlers ([exn? handler])
             (finitize δs bw fmap)
             (solver-assert solver (for/list ([δ δs]) (hash-ref fmap δ)))
             (set! φs (append δs φs)) 
             (let inner ()
               (define fsol (complete (solver-check solver) fmap))
               (define sol (unfinitize fsol fmap))
               (cond [(unsat? sol)
                      (solver-shutdown solver) 
                      (custodian-shutdown-all cust) 
                      (clear-terms! ; Purge finitization terms from the cache
                       (for/list ([(t ft) fmap] #:when (and (term? ft) (not (eq? t ft)))) ft))
                      sol]
                     [(all-true? φs sol) (outer (yield sol))]
                     [else  
                      (solver-assert solver (list (apply || (for/list ([(c v) (model fsol)]) (! (@equal? c v))))))
                      (inner)]))))))                      
      (generator (φs)
       (let loop ([φs φs])
         (with-handlers ([exn? handler])
           (solver-assert solver φs)
           (define sol (solver-check solver))
           (cond [(unsat? sol) (solver-shutdown solver) (custodian-shutdown-all cust) sol]
                 [else (loop (yield sol))]))))))

  
; Extracts an unsatisfiable core for the conjunction 
; of the given formulas, using the provided solver and 
; bitwidth.  The solver and the bitwidth are, by default, 
; current-solver and current-bitwidth.  This procedure assumes 
; that the formulas are unsatisfiable.  If not, an error is thrown. 
; The procedure clears the solver's state before and after use.
(define (∃-debug φs #:solver [solver (current-solver)] #:bitwidth [bw (current-bitwidth)])
  (solver-clear solver)
  (begin0
    (with-handlers ([exn? (lambda (e) (solver-shutdown solver) (raise e))])
      (cond 
        [bw 
         (parameterize ([term-cache (hash-copy (term-cache))])
           (define fmap (finitize φs bw))
           (solver-assert solver (for/list ([φ φs]) (hash-ref fmap φ)))
           (unfinitize (solver-debug solver) fmap))]
        [else 
         (solver-assert solver φs)
         (solver-debug solver)]))
    (solver-clear solver)))
  
; Solves the exists-forall problem for the provided list of inputs, assumptions and assertions. 
; That is, if I is the set of all input symbolic constants, 
; and H is the set of the remaining (non-input) constants appearing 
; in the assumptions and the assertions, 
; this procedure solves the following constraint: 
; ∃H . ∀I . assumes => asserts.
; Note, however, that the procedure will *not* produce models that satisfy the above 
; formula by making assumes evaluate to false.
(define (∃∀-solve inputs assumes asserts #:solver [solver z3] #:bitwidth [bw (current-bitwidth)])
  (parameterize ([current-custodian (make-custodian)]
                 [current-subprocess-custodian-mode 'kill]
                 [term-cache (hash-copy (term-cache))])
    (with-handlers ([exn? (lambda (e) (custodian-shutdown-all (current-custodian)) (raise e))])
      (begin0 
        (cond 
          [bw
           (define fmap (finitize (append inputs assumes asserts) bw))
           (define fsol (cegis (for/list ([i inputs])  (hash-ref fmap i))
                               (for/list ([φ assumes]) (hash-ref fmap φ))
                               (for/list ([φ asserts]) (hash-ref fmap φ))
                               (solver) (solver)))
           (unfinitize fsol fmap)]
          [else 
           (cegis inputs assumes asserts (solver) (solver))])
        (custodian-shutdown-all (current-custodian))))))
         

; Uses the given solvers to solve the exists-forall problem 
; for the provided list of inputs, assumptions and assertions. 
; That is, if I is the set of all input symbolic constants, 
; and H is the set of the remaining (non-input) constants appearing 
; in the assumptions and the assertions, 
; this procedure solves the following constraint: 
; ∃H . ∀I . assumes => asserts.
; Note, however, that the procedure will *not* produce models that satisfy the above 
; formula by making assumes evaluate to false.
(define (cegis inputs assumes asserts guesser checker)
  
  (define φ   (append assumes asserts))
  
  (define ¬φ `(,@assumes ,(apply || (map ! asserts))))
   
  (define trial 0)
  
  (define (guess sol)
    (solver-assert guesser (evaluate φ sol))
    (match (solver-check guesser)
      [(model m) (sat (for/hash ([(c v) m] #:unless (member c inputs)) (values c v)))]
      [other other]))
  
  (define (check sol)
    (solver-clear checker)
    (solver-assert checker (evaluate ¬φ sol))
    (match (solver-check checker)
      [(? sat? m) (sat (for/hash ([i inputs]) (values i (value-for (m i)))))]
      [other other]))
    
  (let loop ([candidate (begin0 (guess (sat)) (solver-clear guesser))])
    (cond 
      [(unsat? candidate) candidate]
      [else
        (let ([cex (check candidate)])
          (cond 
            [(unsat? cex) candidate]
            [else (set! trial (add1 trial))
                  (loop (guess cex))]))])))
             

(define (value-for v)
  (if (constant? v)
      (match (get-type v)
        [(== @boolean?) #f]
        [(or (== @integer?) (== @real?)) 0]
        [(? bitvector? t) (bv 0 t)])
      v))
        
        
